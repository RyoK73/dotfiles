# ===
# env
# ===

export EDITOR="nvim"
# ===
# Lines configured by zsh-newuser-install
# ===

HISTFILE=~/.histfile
HISTSIZE=10000
SAVEHIST=10000
bindkey -v
# End of lines configured by zsh-newuser-install

# ===
# setopt
# ===

setopt auto_cd
setopt PROMPT_SUBST

# ===
# zinit
# ===

ZINIT_HOME="${XDG_DATA_HOME:-$HOME/.local/share}/zinit/zinit.git"
if [[ ! -d $ZINIT_HOME ]]; then
	mkdir -p "$(dirname $ZINIT_HOME)"
	git clone https://github.com/zdharma-continuum/zinit.git "$ZINIT_HOME"
fi

source "${ZINIT_HOME}/zinit.zsh"

autoload -Uz compinit && compinit
zinit light zsh-users/zsh-completions

zinit light Aloxaf/fzf-tab # tabでファイル検索

# aliasをexpand
export ZPWR_EXPAND_TO_HISTORY=true
# export ZPWR_EXPAND_PRE_EXEC_NATIVE=true
zinit light MenkeTechnologies/zsh-expand
# ===
# キーバインディング
# ===
#
## fzf
#
## CTRL+Tの上下移動をTab/Shift+Tabで
export FZF_CTRL_T_OPTS="
  --bind='tab:down'
  --bind='shift-tab:up'
"
## CTRL+Rの上下移動をTab/Shift+Tabで
export FZF_CTRL_R_OPTS="
  --bind='tab:down'
  --bind='shift-tab:up'
"

# zsh

bindkey "^A" autosuggest-accept
bindkey "^P" forward-word

source /usr/share/fzf/key-bindings.zsh
source /usr/share/fzf/completion.zsh
# zinit snippet OMZ::plugins/git/git.plugin.zsh # OMZのgitプラグインを追加する

zinit light zsh-users/zsh-autosuggestions
zinit light zsh-users/zsh-syntax-highlighting

export PATH="$HOME/.local/bin:$PATH"

# ===
# プロンプトをグラフィカルにスタイリング
# ===
# --- prompt helpers ---

parse_git_count() {
	git status --porcelain 2>/dev/null | grep -cE "^ M|^\?\?|^ D"
}

_git_prompt() {
	local branch
	branch=$(git branch 2>/dev/null | grep '^*' | sed 's/^\* //')
	[[ -z "$branch" ]] && return
	if [[ $(git status -s 2>/dev/null) ]]; then
		echo -n "%K{237}%F{#fbd254}%B  ${branch} 🔀 $(parse_git_count) %b%f%k"
	else
		echo -n "%K{237}%F{#57f2c1}%B  ${branch} ✅ 0 %b%f%k"
	fi
}

_venv_info() {
	[[ -n "$VIRTUAL_ENV" ]] && echo -n "%K{#00c666}%F{#343534} ${VIRTUAL_ENV##*/} %f%k"
}

export VIRTUAL_ENV_DISABLE_PROMPT=1

PROMPT='$(_venv_info)%K{#00c666}%F{#343534} %n@%m %f%k%K{#59a4ff}%F{#343534} 🧭 %~ %f%k$(_git_prompt)
%B%F{#00c666}╰> $ %f%b'

ZSH_HIGHLIGHT_STYLES[argument]=$(fg=#ffffff)
ZSH_HIGHLIGHT_STYLES[string]=$(fg=#ffffff)
ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE='fg=#ffffff:waybar'

# ===
# pnpm
# ===

export PNPM_HOME="$HOME/.local/share/pnpm"
case ":$PATH:" in
*":$PNPM_HOME:"*) ;;
*) export PATH="$PNPM_HOME:$PATH" ;;
esac

alias killport="fuser -k 3000/tcp 2>/dev/null"

function pnpm-dev() {
	fuser -k 3000/tcp 2>/dev/null
	pnpm dev
}

# ===
# Zsh
# ===
alias -s {md,lua,ts,tsx,js,jsx,json,jsonc,conf,toml,yaml,yml,toml,html,css,zshrc}=$EDITOR

alias ls="ls -a1"
alias lsc="ls -a1 | bat"
alias cat="bat"

# 指定 or カレントディレクトリをツリー表示する
function trc() {
	local dir="$1"
	local flg
	if [[ -z "$dir" ]]; then
		dir="."
	else
		flg="-a"
	fi
	tree "$dir" "$flg" | cat
}
alias xopen="xdg-open"

alias cdnv="cd $HOME/.config/nvim/lua/plugins"

# ディレクトリが存在しないファイルの場合、ディレクトリごと作成するtouch
function mktouch() {
	if [[ -z "$1" ]]; then
		echo "引数が必要です"
		exit
	fi
	local filepath="$1"
	local dir=$(dirname "$filepath")
	if [[ ! -d "$dir" ]]; then
		mkdir -p "$dir"
	fi
	touch "$filepath"
}

# catしたファイルの内容をコピーする
function catc() {
	cat "$1" | wl-copy
}

# ===
# helix
# ===

alias hx="helix"

# ===
# Neovim・Nvim
# ===
alias nv="nvim"
# ===
# claude code
# ===

function cc() {
	if [[ -z "$1" ]]; then
		echo "セッション名を指定してください"
		echo "例：cc session-name"
		return 1
	fi
	wl-copy "$1"
	claude --name "$1"
}

function cct() {
	if [[ -z "$1" ]]; then
		echo "セッション名を指定してください"
		echo "例：cc session-name"
		return 1
	fi
	claude /think --name "think-${1}"
}

alias ccr="claude -r"
alias ccc="claude -c"

function skills-tutor() {
	if [[ -f "$HOME/.claude/skills-tutor.md" ]]; then
		cat "$HOME/.claude/skills-tutor.md"
	else
		echo "$HOME/.claude/skills-tutor.mdがありません"
	fi
}

# ===
# Git
# ===
alias gsw="git switch"
alias gswc="git switch -c"

alias ga="git add --verbose"
alias gap="git add --verbose -p"

alias gc="git commit --verbose"
alias gcm="git commit -m"
alias gcp="git commit -p"
alias gcam="git commit --amend"
alias gcamn="git commit --amend --no-edit"

alias gst="git status"

alias gps="git push"
alias gps-r="git push --force-with-lease" # ローカルが最新の場合のみgit push --forceが成功する

alias gpl-r="git pull --rebase"

alias gd="git diff"

alias gss="git stash push -m"
alias gsp="git stash pop"
alias gsl="git stash list"

# プロジェクトルートへ移動する
# aliasだと$()がターミナル起動時にキャッシュされてエラーが表示されるためfunctionで記述
function gcd() {
	cd $(git rev-parse --show-toplevel)
}

# Git remoteでマージ済みのローカルブランチを削除する
function git-cleanup() {
	git fetch --prune
	git branch -vv | grep ': gone]' | awk '{print $1}' | xargs git branch -d
	echo
	echo 💫残りのローカルブランチ💫
	echo
	git branch
}

# merge済みのgit worktreeを削除する
# ディレクトリの削除・ブランチの削除
function gitc() {
	git fetch --prune
	git branch -vv | grep ': gone' | awk '{
    if ($1 == "+") {
      gsub(/[()]/,"",$4); print $2,$4
    } else { print $1,"" }
  }' | while read branch wt_path; do
		[[ -n "$wt_path" ]] && git worktree remove "$wt_path"
		git branch -D "$branch"
	done
	echo
	echo 💫残りのローカルブランチ💫
	echo
	git branch
}

# どこでもgit issue
# dev/confにあるテンプレートはconfigで管理
function giti() {
	trap 'return 130' 2
	local dir=$(find ~/dev -maxdepth 1 -type d | fzf --prompt "cd: " --preview 'ls {}')
	[ -n "$dir" ] && cd "$dir"
	gh issue create -F ~/dev/conf/1-idea.md -e -t"idea: "
}

# git worktree

function gitp() {
	if ! git rev-parse --git-dir &>/dev/null; then
		echo "not a git directory"
		return
	elif ! git rev-parse HEAD --git-dir &>/dev/null; then
		echo "commitがありません"
		return
	elif [[ -z "$1" ]]; then
		echo "ブランチ名を指定してください"
		return
	fi

	git pull --rebase origin main

	local repo
	repo=$(basename $(git remote get-url origin) .git) || repo=$(basename $(git rev-parse --show-toplevel)) # ディレクトリではなく、remote repositoryを基準にprefixを決定、remote repositoryがなければディレクトリ名にフォールバック

	local branchdir="../$repo-$1"
	git worktree add -b "$1" "$branchdir" # mainからではなく現ブランチから生成する
	cd "$branchdir"

	git push # 作成したブランチのままremoteへpush(要upstream)

	if [[ -n "$2" ]]; then
		bash "$HOME/.claude/skills/project-sync/scripts/sync.sh" start "$2"
	fi
}

# ===
# dotfiles用/Config
# ===

function config() {
	if [[ "$1" == "add" && ("$2" == "." || "$2" == "-A") ]]; then
		echo "HOME すべてを追跡することになるため、config add . / -A は無効化されています。"
		return 1
	fi
	git --git-dir=$HOME/.dotfiles/ --work-tree=$HOME "$@"
}

alias coa="config add" # dotfiles commit -m
alias coap="config add -p"
alias coc="config commit" # dotfiles commit -m
alias coca="config commit -a"
alias cocp="config commit -p"
alias crh="config reset HEAD"
function coac() {
	config add "$1" && config commit

}
function coacu() {
	if [[ -d "$1" ]]; then
		config add -u "$1" && config commit
	else
		echo "$1はディレクトリではありません"
	fi
}
function coacup() {
	if [[ -d "$1" ]]; then
		config add -up "$1" && config commit
	else
		echo "$1はディレクトリではありません"
	fi
}
alias cocm="config commit -m"   # dotfiles commit -m
alias cocma="config commit -am" # dotfiles commit -am : 追跡しているファイルの変更をaddしてcommitする
alias cost="config status"      # dotfiles status : ステータス確認
alias cop="config push origin main"

# ===
# LIFE用
# ===

function life-tutor() {
	if [[ -d "$HOME/dev/LIFE" ]]; then
		cat "$HOME/dev/LIFE/README.md"
	else
		echo "${HOME}/dev/LIFEがありません"
	fi
}

# ===
# ショートカットalias
# ===
alias waybar-reload="pkill waybar && waybar & disown"
alias hypr-reload="hyprctl reload"
alias mozc-setup="fcitx5-config-qt"

# pnpm
alias npm="pnpm"
export PNPM_HOME="/home/taruroma/.local/share/pnpm"
case ":$PATH:" in
*":$PNPM_HOME/bin:"*) ;;
*) export PATH="$PNPM_HOME/bin:$PATH" ;;
esac
# pnpm end

# gists-tips-manager
source "/home/taruroma/dev/gists-tips-manager/scripts/gists-tips-manager.zsh"

# claude-tasks
export CLAUDE_TASKS_HOME="/home/taruroma/dev/claude-tasks"
export PATH="$HOME/.local/bin:$PATH"

# golang
export PATH="$(go env GOPATH):$PATH"
